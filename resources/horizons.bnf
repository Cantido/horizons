(* Overall document structure *)

S ::= geophysical-document | ephemeredes-document;

<geophysical-document> ::=
    <garbage>,
    <separator>, file-header,
    (<ws>        geophysical-data)?
    (<ws>        physical-properties)?
    (<ws>        dynamical-characteristics)?
    <separator> <garbage> <select-prompt>;

<ephemeredes-document> ::=
    <ws "Working" " ...", spinning-bar>
    <garbage>
    <separator> <ephemeris-header>
    <separator> <targeting>
    <separator> time-frame
    <separator> <ephemeris-metadata>
    <separator> <ephemeris-legend>
    <separator> ephemeredes
    <separator> <coordinate-system-description>
    <separator>
    <garbage>
    <select-prompt>;

spinning-bar ::= (forward-slash | pipe | back-slash | hyphen | ws | backspace)+;

forward-slash ::= "/";
pipe ::= "|";
back-slash ::= "\\";
hyphen ::= "-";

ephemeris-header ::=
"Ephemeris / PORT_LOGIN" ws date ws time ws year ws "Pasadena, USA    / Horizons"

targeting ::=
"Target body name:" ws body-name ws "(" body-id ")" ws "{source: " #"[\w]+" "}" ws
"Center body name:" ws body-name ws "(" body-id ")" ws "{source: " #"[\w]+" "}" ws
"Center-site name: BODY CENTER"

time-frame ::= start-time, stop-time, step-size;

start-time ::= <"Start time      : "> timestamp <ws>;
stop-time ::= <"Stop  time      : "> timestamp <ws>;
step-size ::= <"Step-size       : "> duration <ws>;

ephemeris-metadata ::=
"Center geodetic : 0.00000000,0.00000000,0.0000000 {E-lon(deg),Lat(deg),Alt(km)}" ws
"Center cylindric: 0.00000000,0.00000000,0.0000000 {E-lon(deg),Dxy(km),Dz(km)}" ws
"Center radii    : 6378.1 x 6378.1 x 6356.8 km     {Equator, meridian, pole}" ws
"Output units    : " ("KM-D" | "AU-D") ws
"Output type     : GEOMETRIC cartesian states" ws
"Output format   : 3 (position, velocity, LT, range, range-rate)" ws
"Reference frame : ICRF/J2000.0" ws
"Coordinate " ("systm" | "system description" ) ": Ecliptic and Mean Equinox of Reference Epoch";

ephemeris-legend ::=
"JDTDB" ws "X" ws "Y" ws "Z" ws "VX" ws "VY" ws "VZ" ws "LT" ws "RG" ws "RR";

ephemeredes ::=
<"$$SOE"> <ws> (ephemeris <ws>)+ <"$$EOE">;

ephemeris ::=
    julian-day-number <equals> timestamp
    <ws> x-position <ws> y-position <ws> z-position
    <ws> x-velocity <ws> y-velocity <ws> z-velocity
    <ws> lt <ws> range <ws> range-rate;

julian-day-number ::= float;

x-position ::= sci-not;
y-position ::= sci-not;
z-position ::= sci-not;

x-velocity ::= sci-not;
y-velocity ::= sci-not;
z-velocity ::= sci-not;

lt ::= sci-not;
range ::= sci-not;
range-rate ::= sci-not;

coordinate-system-description ::=
"Coordinate system description:" ws

"  Ecliptic and Mean Equinox of Reference Epoch" ws

"    Reference epoch: J2000.0" ws
"    XY-plane: plane of the Earth's orbit at the reference epoch" ws
"              Note: obliquity of 84381.448 arcseconds wrt ICRF equator (IAU76)" ws
"    X-axis  : out along ascending node of instantaneous plane of the Earth's" ws
"              orbit and the Earth's mean equator at the reference epoch" ws
"    Z-axis  : perpendicular to the xy-plane in the directional (+ or -) sense" ws
"              of Earth's north pole at the reference epoch." ws

"  Symbol meaning [" "1 au= 149597870.700 km, "? "1 day=" ws? "86400.0 s]:" ws

"    JDTDB    Julian Day Number, Barycentric Dynamical Time" ws
"      X      X-component of position vector (" ("km" | "au") ")" ws
"      Y      Y-component of position vector (" ("km" | "au") ")" ws
"      Z      Z-component of position vector (" ("km" | "au") ")" ws
"      VX     X-component of velocity vector (" ("km" | "au") "/day)" ws
"      VY     Y-component of velocity vector (" ("km" | "au") "/day)" ws
"      VZ     Z-component of velocity vector (" ("km" | "au") "/day)" ws
"      LT     One-way down-leg Newtonian light-time (day)" ws
"      RG     Range; distance from coordinate center (" ("km" | "au") ")" ws
"      RR     Range-rate; radial velocity wrt coord. center (" ("km" | "au") "/day)" ws

"Geometric states/elements have no aberrations applied." ws

" Computations by ..." ws
"     Solar System Dynamics Group, Horizons On-Line Ephemeris System" ws
"     4800 Oak Grove Drive, Jet Propulsion Laboratory" ws
"     Pasadena, CA  91109   USA" ws
"     Information: http://ssd.jpl.nasa.gov/" ws
"     Connect    : telnet://ssd.jpl.nasa.gov:6775  (via browser)" ws
"                  http://ssd.jpl.nasa.gov/?horizons" ws
"                  telnet ssd.jpl.nasa.gov 6775    (via command-line)" ws
"     Author     : Jon.D.Giorgini@jpl.nasa.gov"


(* General data types *)

<garbage> ::= (#"[\d\[\?1h\= \^\>l]" | "K" | escape | <ws>)+;
separator ::= <ws>, (short-sep | long-sep), <ws>, separator?;
short-sep ::= #"\*{79}";
long-sep ::= #"\*{110}";

space ::= #"[\s\n\r]";
ws ::= space*;
backspace ::= "\u0008";
escape ::= "\u001B"
delete ::= "\u007F";

<digit> ::= #"\d";
integer ::= #"[-\+]?\d+" | comma-separated-integer;
comma-separated-integer ::= #"\d?\d?\d?,(\d\d\d,)+\d\d\d";
<integer-with-error> ::= #"\d+\(?\+\-\d+\)?";
float ::= #"\-?\d*\.\d*";
<float-with-error> ::= (#"\d*\.\d*\(\d\+\-\d\)") | (#"\d*\.\d*\s?\+\-\s?\d*\.\d*");
<approximate-integer> ::= #"~\d+";

sci-not ::= significand <ws "("? sci-not-separator> exponent <")"?>
<sci-not-separator> := (("x" | "*") ws "10^") | "E";
significand ::= float | integer;
exponent ::= integer;

timestamp ::= era?, <space>, date, <ws>, time, <space>, time-zone?;

date ::= (year <"-"> month <"-"> day) |
         (month <" "+> day <", "> year) |
         (day-of-week <" "+> month <" "+> day);

era ::= "A.D."
year ::= integer;
month ::= "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec";
day ::= integer
time ::= hour-of-day <":"> minute-of-hour (<":"> second-of-minute (<"."> millisecond-of-second)?)?;
day-of-week ::= "Mon" | "Tue" | "Wed" | "Thu" | "Fri" | "Sat" | "Sun";

hour-of-day ::= integer;
minute-of-hour ::= integer;
second-of-minute ::= integer;
millisecond-of-second ::= integer;

(* TODO: property parse a seconds measurement like 22.4, currently this things there are 4 milliseconds *)
duration ::= days? (hours <ws? ("h" | "hr") ws>)? (minutes <ws? ("m" | "minutes") ws?>)? (seconds <"."> milliseconds <"s">)?;
days ::= (float | integer) <ws? "d" ws?>;
hours ::= float | float-with-error | integer;
minutes ::= integer;
seconds ::= integer;
milliseconds ::= integer;

time-zone ::= "UT" | "TDB";

(* Units using the UN/CEFACT Common Codes for Units of Measurement
    See http://www.unece.org/fileadmin/DAM/cefact/recommendations/rec20/rec20_Rev9e_2014.xls
*)

unit-D54 ::= "W/m^2" | "Wm^2"; (* [sic] *)
unit-M62 ::= "("? ("km/s" | "km s^-1") ")"?;
unit-KEL ::= "K";

(* Domain-specific data types *)

body-name ::= #"\w+";
body-id ::= integer;

(* Document proper *)

file-header ::=
    revision-date   <space+>
    body-name       <space+>
    body-id         <space+>
    <#"\/ \d"?>

revision-date ::= <"Revised: "> date;

dynamical-characteristics ::= <dynamical-characteristics-header>  (<ws> geophysical-field)+;
geophysical-data ::=  <geophysical-data-header> (<ws> geophysical-field)+;
physical-properties ::= <physical-properties-header>  (<ws> geophysical-field)+;

<geophysical-field> ::=
    <"Mass layers:"> |
    <obliquity-source> |
    <surface-area-title> |
    a-roche-ice |
    atmospheric-mass |
    atmospheric-pressure |
    core-radius |
    crust-mass |
    density |
    dipole-tilt-offset |
    equatorial-gravity |
    equatorial-radius |
    escape-velocity |
    fig-offset |
    flattening |
    fluid-core-radius |
    g-equatorial |
    g-o |
    g-polar |
    geometric-albedo |
    gm-1-sigma |
    grav-spectral-fact |
    heat-flow-mass |
    hill-sphere-radius |
    hydrostatic-flattening |
    inferred-rotation-period |
    inner-core-mass |
    inner-core-radius |
    j2 |
    ks |
    m |
    magnetic-moment |
    mantle-mass |
    mass |
    mass-ratio-from-sun |
    maximum-angular-diameter |
    mean-daily-motion |
    mean-radius |
    mean-sidereal-orbital-period-days |
    mean-sidereal-orbital-period-years |
    mean-solar-day |
    mean-temperature |
    moment-of-inertia |
    monent-of-inertia-upper-bound |
    obliquity-to-orbit |
    ocean-mass |
    offset |
    orbit-velocity |
    outer-core-mass |
    polar-axis |
    polar-gravity |
    polar-radius |
    potential-love-k2 |
    rocky-core-mass |
    rotation-rate |
    semi-major-axis |
    sidereal-rotation-period |
    solar-constant |
    standard-gravitational-parameter |
    surface-area-land |
    surface-area-sea |
    topo-spectral-fact |
    visual-magnitude |
    visual-magnitude-opposition |
    volume |
    y-factor;

equals ::= space* "=" space*;

geophysical-data-header ::= "GEO"? "PHYSICAL DATA" ws "(update" "d"? ws date "):";
dynamical-characteristics-header ::= "DYNAMICAL CHARACTERISTICS:";
physical-properties-header ::= "PHYSICAL PROPERTIES (revised " date "):";

a-roche-ice ::= <"A_roche(ice)/Rp" | "Aroche(ice)/Rp"> <equals> float;
atmospheric-mass ::= <"Atmos"> <equals> sci-not <" kg">;
atmospheric-pressure ::= <("Atm." | "Atmos.") " pressure" " (bar)"?> <equals> (float | integer)? <" bar"?>;
core-radius ::= <"Core radius (km)"> <equals> approximate-integer;
crust-mass ::= <"crust"> <equals> sci-not <" kg">;
density ::= <"Density", (", gm cm^-3" | " (g cm^-3)" | " (g/cm^3)")> <equals> (float | float-with-error);
dipole-tilt-offset ::= <"Dipole tilt/offset"> <equals> #"\d+\.?\d*de?g/\d+\.\d+Rp"
equatorial-gravity ::= <"Equ. gravity  ms^-2"> <equals> float;
equatorial-radius ::= <"Equatorial Radius, Re" | "Equ. radius, km" | "Equat. radius (1 bar)"> <equals> (integer | integer-with-error | float | float-with-error) <" km"?>;
escape-velocity ::= <"Escape" ws ("vel." | "velocity") ws? unit-M62?> <equals> float <ws> <unit-M62?>;
fig-offset ::= <"Fig. offset (Rcf-Rcm)"> <equals> (float-with-error | "0.19+-01") <" km"?>;
flattening ::= <"Flattening" ", f"?> <equals> (#"\d/\d\d\d\.\d\d\d" | float)?;
fluid-core-radius ::= <"Fluid core rad"> <equals> integer <" km">;
(* equatorial & polar gravity? units seem right. What is g-o? *)
g-equatorial ::= <"ge, m s^-2 (equatorial)" | "Equ. grav, ge (m/s^2)"> <equals> float;
g-o ::= <"go, m s^-2               = "> float;
g-polar ::= <"gp, m s^-2 (polar)" | "Pol. grav, gp (m/s^2)"> <equals> (float | float-with-error);
geometric-albedo ::= <"Geometric albedo"> <equals> float;
gm-1-sigma ::= <"GM 1-sigma" ws? "(" ws? ( "km^3 s^-2" | "km^3/s^2" ) ws? ")"> <equals> #"\+\- ?\d\.?\d+";
grav-spectral-fact ::= <"Grav spectral fact u"> <equals> (sci-not | float);
heat-flow-mass ::= <"Heat flow/mass (x10^7)"> <equals>  integer? <ws "erg/gm*s">?;
hill-sphere-radius ::= <"Hill's sphere" ws ("rad." | "radius") ","? ws "Rp"?> <equals> (float | integer);
hydrostatic-flattening ::= <"Hydrostatic flat., fh"> <equals> float;
inferred-rotation-period ::= <"Inferred rot. period"> <equals> duration;
inner-core-mass ::= <"inner core     = "> sci-not <" kg">;
inner-core-radius ::= <"Inner core rad   = "> integer <" km">;
j2 ::= <"J2  (GEM T2, 1990)       = "> float;
ks ::= <"ks = 3*J2/m"> <equals> float;
m ::= <"m = w^2a^3/GM"> <equals> float;
magnetic-moment ::= <"Mag. mom (gauss Rp^3)" | "Magnetic moment" | "Mag.dip.mom(gauss-Rp^3)"> <equals> (#"< 1x10\^\-\d" | (float <ws "gauss Rp^3">?));
mantle-mass ::= <"mantle         = "> sci-not <" kg">;
mass ::= <"Mass" ","? " " "("? "10^"> exponent <" "? "kg" " "? ")"?> <equals> (float | float-with-error);
mass-ratio-from-sun ::= <"Mass ratio " ("(Sun/" body-name ")" | "(sun/plnt)")> <equals> (integer | integer-with-error | float);
maximum-angular-diameter ::= <"Max. angular diam."> <equals> #'\d+\.\d+\"';
mean-daily-motion ::= <"Mean daily motion" ", n"?> <equals> float <ws> <"deg/d" | "dg/d">;
mean-radius ::= <(("Mean radius" ws ", "? "("? "km" ")"?) | "Volumetric mean radius")> <equals> (float-with-error | integer-with-error) <ws "km">?;
<mean-sidereal-orbit-label> ::= <"Mean"? ws ("sidereal" | "Sidereal") ws (("orb" "."?)  | "orbit")? ws? (("per" "."?) | "period")>;
mean-sidereal-orbital-period-years ::= <mean-sidereal-orbit-label> <equals> float <ws> <("yrs" | "y" | "yr")?>;
mean-sidereal-orbital-period-days ::= <mean-sidereal-orbit-label> <equals> float <ws> <("d" | "days")>;
mean-solar-day ::= <"Mean solar day" ", days"?> <equals> float <" d"?>;
mean-temperature ::= <("Atmos. temp. (1 bar)") | ("Mean Temperature" (" (" | ", ") unit-KEL ")"?)> <equals> (integer | integer-with-error)? <ws> <unit-KEL?>;
moment-of-inertia ::= <("Mom. of Inertia" | "Moment of inertia" | "Mom. of inert. I/MRo^2")> <equals> float?;
monent-of-inertia-upper-bound ::= <"I/MRo^2 (upper bound)"> <equals> float;
ocean-mass ::= <"oceans"> <equals> sci-not <" kg">;
obliquity-to-orbit ::= <"Obliquity to orbit"> <"[1]"?> <", deg"?> <equals> (float | #"\d.\d\d' \+/\- \d\.\d'") <" deg"?>;
offset ::= <"Offset (lat./long.)"> <equals> #"\d+d? ?\/ ?\d+d?" <ws "dg/dg">?;
orbit-velocity ::= <"Mean"? ws ("Orbit" | "orbit") ws ("vel." | "velocity") ","? ws unit-M62?> <equals> float <ws> <unit-M62?>;
outer-core-mass ::= <"outer core     = "> sci-not <" kg">;
polar-gravity ::= <"Polar gravity ms^-2"> <equals> float?;
polar-axis ::= <"Polar axis, km"> <equals> float;
polar-radius ::= <"Polar radius (km)"> <equals> integer-with-error;
potential-love-k2 ::= <("Potential Love # k2" | "Love no., k2")> <equals> <"~"?> (float | float-with-error)?;
rocky-core-mass ::= <"Rocky core mass (Mc/M)"> <equals> float?;
rotation-rate ::= <("Rot. Rate (x10^5 s)" | "Mean rot. rate, rad s^-1" | "Rot. rate(10^-4 rad/s)")> <equals> (float | sci-not);
semi-major-axis ::= <"Semi-major axis"> <equals> integer-with-error?;
solar-constant ::= <("Solar constant, " unit-D54) | "Planetary Solar Const"> <equals> float  <ws "("? unit-D54 ")"?>?;
sidereal-rotation-period ::= <("Sidereal rot. period" | "Sidereal period, hr" | "Sidereal period" | "Rotation period")> <equals> (duration | float);
standard-gravitational-parameter ::= <"GM" (", km^3 s^-2" | " (km^3 s^-2)" | " (km^3/s^2)" )> <equals> (float | integer);
surface-area-title ::= "Surface Area:";
surface-area-land ::= <"land           = "> sci-not <" km">;
surface-area-sea ::= <"sea            = "> sci-not <" km">;
topo-spectral-fact ::= <"Topo. spectral fact t"> <equals> (sci-not | integer);
visual-magnitude ::= <("Visual" | "Vis.") ws ("mag." | "magnitude") ws "V(1,0)"> <equals> float;
visual-magnitude-opposition ::= <"Vis. mag. (opposition)"> <equals> <"+"?> float;
volume ::= < "Volume" ","? " " "("? "x"? "10^"> exponent <" km^3" ")"?> <equals> float;
y-factor ::= <"Y factor (He/H ratio)"> <equals> float-with-error?;

obliquity-source ::= "[1] Margot et al., Science 316, 2007";

select-prompt ::= ("Select ... [E]phemeris, [F]tp, [M]ail, [R]edisplay, ?, <cr>: " |
                   ">>> Select... [A]gain, [N]ew-case, [F]tp, [K]ermit, [M]ail, [R]edisplay, ? : ");

