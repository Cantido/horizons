(* Overall document structure *)

S ::=
    <separator>, file-header,
    <ws>,        geophysical-data,
    <separator>, ephemeris-metadata,
    <separator>, targeting,
    <separator>, time-frame,
    <separator>, constants,
    <separator>, ephemeris-header,
    <separator>, ephemeris,
    <separator>, <footer>;



(* General data types *)

separator ::= (short-sep | long-sep), <ws>, separator?;
short-sep ::= #"\*{79}";
long-sep ::= #"\*{110}";

space ::= #"\s";
ws ::= space*;

<digit> ::= #"\d";
<integer> ::= #"\d+";
<integer-with-error> ::= #"\d+\+\-\d+";
<float> ::= #"\-?\d*\.\d*";
<float-with-error> ::= (#"\d*\.\d*\(\d\+\-\d\)") | (#"\d*\.\d*\s?\+\-\s?\d*\.\d*");
<approximate-integer> ::= #"~\d+";

<timestamp> ::= era?, <space>, date, <ws>, time, <space>, time-zone?;

<date> ::= (year <"-"> month <"-"> day) | (month <" "> day <", "> year);
<duration> ::= "1440 minutes";

era ::= "A.D."
year ::= integer;
month ::= "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec";
day ::= #"[0-9]{2}";
time ::= #"\d\d:\d\d(\:\d\d\.\d\d\d\d)?";
time-zone ::= "UT"



(* Domain-specific data types *)

body-name ::= "Mars" | "Earth";
body-id ::= "499" | "399";
<spectral-fact> ::= #"\d\d \(x10\^\d\)";



(* Document proper *)

file-header ::=
    revision-date   <space+>
    body-name       <space+>
    body-id
    <" / 4">;

revision-date ::= <"Revised: "> date;


geophysical-data ::=
    <ws> geophysical-data-header,
    <ws> mean-radius,
    <ws> density,
    <ws> mass,
    <ws> flattening,
    <ws> volume,
    <ws> semi-major-axis,
    <ws> sidereal-rotation-period
    <ws> rotation-rate
    <ws> mean-solar-day
    <ws> polar-gravity
    <ws> moment-of-inertia
    <ws> equatorial-gravity
    <ws> core-radius
    <ws> potential-love-k2

    <ws> grav-spectral-fact
    <ws> topo-spectral-fact
    <ws> fig-offset
    <ws> offset
    <ws> standard-gravitational-parameter
    <ws> equatorial-radius
    <ws> gm-1-sigma
    <ws> mass-ratio-from-sun

    <ws> atmospheric-pressure
    <ws> maximum-angular-diameter
    <ws> mean-temperature
    <ws> visual-magnitude
    <ws> geometric-albedo
    <ws> obliquity-to-orbit
    <ws> mean-sidereal-orbital-period
    <ws> orbit-velocity
    <ws> mean-sidereal-orbital-period
    <ws> escape-velocity
    <ws> hill-sphere-radius
    <ws> magnetic-moment
    <ws>;

equals ::= space* "=" space*;

geophysical-data-header ::= "GEOPHYSICAL DATA (updated 2009-May-26):"

atmospheric-pressure ::= <"Atmos. pressure (bar)"> <equals> float;
core-radius ::= <"Core radius (km)"> <equals> approximate-integer;
density ::= <"Density (g cm^-3)"> <equals> float-with-error;
equatorial-gravity ::= <"Equ. gravity  ms^-2"> <equals> float;
equatorial-radius ::= <"Equatorial Radius, Re"> <equals> float <" km">;
escape-velocity ::= <"Escape vel. km/s"> <equals> float;
fig-offset ::= <"Fig. offset (Rcf-Rcm)"> <equals> float-with-error <" km">;
flattening ::= <"Flattening, f"> <equals> #"\d/\d\d\d\.\d\d\d";
geometric-albedo ::= <"Geometric albedo"> <equals> float;
gm-1-sigma ::= <"GM 1-sigma (km^3 s^-2)"> <equals> #"\+\- \d\.\d";
grav-spectral-fact ::= <"Grav spectral fact u"> <equals> spectral-fact;
hill-sphere-radius ::= <"Hill's sphere rad. Rp"> <equals> float;
magnetic-moment ::= <"Mag. mom (gauss Rp^3)"> <equals> #"< 1x10\^\-\d";
mass ::= <"Mass (10^23 kg )"> <equals> float;
mass-ratio-from-sun ::= <"Mass ratio (Sun/Mars)"> <equals> integer-with-error;
maximum-angular-diameter ::= <"Max. angular diam."> <equals> #'\d+\.\d+\"';
mean-radius ::= <"Mean radius (km)"> <equals> float-with-error;
mean-sidereal-orbital-period ::= mean-sidereal-orbital-period-years | mean-sidereal-orbital-period-days;
<mean-sidereal-orbital-period-years> ::= <"Mean sidereal orb per"> <equals> float <" y">;
<mean-sidereal-orbital-period-days> ::= <"Mean sidereal orb per"> <equals> float <" d">;
mean-solar-day ::= <"Mean solar day"> <equals> float <" d">;
mean-temperature ::= <"Mean Temperature (K)"> <equals> integer;
moment-of-inertia ::= <"Mom. of Inertia"> <equals> float;
obliquity-to-orbit ::= <"Obliquity to orbit"> <equals> float <" deg">;
offset ::= <"Offset (lat./long.)"> <equals> #"\d\dd \/ \d\dd";
orbit-velocity ::= <"Orbit vel.  km/s"> <equals> float;
polar-gravity ::= <"Polar gravity ms^-2"> <equals> float;
potential-love-k2 ::= <"Potential Love # k2"> <equals> float-with-error;
rotation-rate ::= <"Rot. Rate (x10^5 s)"> <equals> float;
semi-major-axis ::= <"Semi-major axis"> <equals> integer-with-error;
sidereal-rotation-period ::= <"Sidereal rot. period"> <equals> float <" hr">;
standard-gravitational-parameter ::= <"GM (km^3 s^-2)"> <equals> float;
topo-spectral-fact ::= <"Topo. spectral fact t"> <equals> spectral-fact;
visual-magnitude ::= <"Visual mag. V(1,0)"> <equals> float;
volume ::= <"Volume (x10^10 km^3)"> <equals> float;

ephemeris-metadata ::=
"Ephemeris / WWW_USER Mon Mar  6 16:56:26 2017 Pasadena, USA      / Horizons" <ws>;

targeting ::= target-body, center-body, center-site;

target-body ::= <"Target body name:"> targeting-body <ws>
center-body ::= <"Center body name:"> targeting-body <ws>
center-site ::= <"Center-site name:"> <space> "GEOCENTRIC" <ws>;

<targeting-body> := <space> body-name <space> <"("> body-id <")"> <space+> source;

source ::= <"{source:"> <space> "mar097" <"}">;

time-frame ::= start-time, stop-time, step-size;

start-time ::= <"Start time      : "> timestamp <ws>;
stop-time ::= <"Stop  time      : "> timestamp <ws>;
step-size ::= <"Step-size       : "> duration <ws>;

colon ::= ws ":" ws;
by ::= ws "x" ws;

constants ::=
    target-pole-equ,
    target-radii,
    center-geodetic,
    center-cylindric,
    center-pole-equ,
    center-radii,
    target-primary,
    visibility-interferer,
    rel-light-bend,
    rel-light-bend-gm,
    atmospheric-refraction,
    ra-format,
    time-format,
    eop-file,
    eop-coverage,
    units-conversion,
    table-cutoffs-1,
    table-cutoffs-2;


target-pole-equ ::= <"Target pole/equ"> <colon> "IAU_MARS" <ws> <"{East-longitude -}"> <ws>;
target-radii ::= <"Target radii"> <colon> equator <by> meridian <by> pole <" km"> <ws> <"{Equator, meridian, pole}"> <ws>;
center-geodetic ::= <"Center geodetic"> <colon> longitude <","> latitude <","> altitude <ws> <"{E-lon(deg),Lat(deg),Alt(km)}"> <ws>;
center-cylindric ::= <"Center cylindric"> <colon> longitude <","> xy-dimension <","> z-dimension <ws> <"{E-lon(deg),Dxy(km),Dz(km)}"> <ws>;
center-pole-equ ::= <"Center pole/equ"> <colon> "High-precision EOP model" <ws> <"{East-longitude +}"> <ws>;
center-radii ::= <"Center radii"> <colon> "6378.1 x 6378.1 x 6356.8 km" <ws> <"{Equator, meridian, pole}"> <ws>;
target-primary ::= <"Target primary"> <colon> "Sun" <ws>;
visibility-interferer ::= <"Vis. interferer"> <colon> "MOON (R_eq= 1737.400) km" <ws> <source> <ws>;
rel-light-bend ::= <"Rel. light bend"> <colon> "Sun, EARTH" <ws> <source> <ws>;
rel-light-bend-gm ::= <"Rel. lght bnd GM"> <colon> "1.3271E+11, 3.9860E+05 km^3/s^2" <ws>;
atmospheric-refraction ::= <"Atmos refraction"> <colon> "NO (AIRLESS)" <ws>;
ra-format ::= <"RA format"> <colon> "HMS" <ws>;
time-format ::= <"Time format"> <colon> "CAL" <ws>;
eop-file ::= <"EOP file"> <colon> "eop.170306.p170528" <ws>;
eop-coverage ::= <"EOP coverage"> <colon> "DATA-BASED 1962-JAN-20 TO 2017-MAR-06. PREDICTS-> 2017-MAY-27" <ws>;
units-conversion ::= <"Units conversion"> <colon> "1 au= 149597870.700 km, c= 299792.458 km/s, 1 day= 86400.0 s" <ws>;
table-cutoffs-1 ::= <"Table cut-offs 1"> <colon> "Elevation (-90.0deg=NO ),Airmass (>38.000=NO), Daylight (NO )" <ws>;
table-cutoffs-2 ::= <"Table cut-offs 2"> <colon> "Solar Elongation (  0.0,180.0=NO ),Local Hour Angle( 0.0=NO )" <ws>;

equator ::= float;
meridian ::= float;
pole ::= float;

longitude ::= float;
latitude ::= float;
altitude ::= float

xy-dimension ::= float;
z-dimension ::= float;

ephemeris-header ::= " Date__(UT)__HR:MN     R.A._(ICRF/J2000.0)_DEC  APmag  S-brt            delta      deldot    S-O-T /r    S-T-O" <ws>;

ephemeris ::= <"$$SOE">, <ws>, ephemeris-line-item+, <"$$EOE">, <ws>;

ephemeris-line-item ::=
    <ws>, measurement-time,
    <ws>, ascension-declination,
    <ws>, apparent-magnitude,
    <ws>, surface-brightness,
    <ws>, range,
    <ws>, range-rate,
    <ws>, sun-observer-target-angle,
    <ws>, sun-observer-target-angle-direction,
    <ws>, sun-target-observer-angle,
    <ws>;

measurement-time ::= timestamp;

ascension-declination ::= #"\d\d \d\d \d\d\.\d\d \+\d\d \d\d \d\d\.\d";

apparent-magnitude ::= #"\d\.\d\d";

surface-brightness ::= #"\d\.\d\d";

range ::= #"\d\.\d{14}";
range-rate ::= #"\d\d\.\d{7}";
sun-observer-target-angle ::= #"\d\d\.\d\d\d\d";

sun-observer-target-angle-direction ::= "/T" | "/L";

sun-target-observer-angle ::= #"\d\d\.\d{4}";

footer ::= "Column meaning:", #"[\s\S\w\W.]*";