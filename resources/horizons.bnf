(* Overall document structure *)

S ::=
    <separator>, file-header,
    <newline>,   geophysical-data,
    <separator>, ephemeris-metadata,
    <separator>, targeting,
    <separator>, time-frame,
    <separator>, constants,
    <separator>, ephemeris-header,
    <separator>, ephemeris,
    <separator>, <footer>;



(* General data types *)

separator ::= (short-sep | long-sep), <whitespace>, separator?;
short-sep ::= #"\*{79}";
long-sep ::= #"\*{110}";

newline ::= "\n";
space ::= #"\s";
whitespace ::= (space* | newline*) whitespace*;

<digit> ::= #"\d";
<integer> ::= #"\d+";
<integer-with-error> ::= #"\d+\+\-\d+";
<float> ::= #"\-?\d*\.\d*";
<float-with-error> ::= (#"\d*\.\d*\(\d\+\-\d\)") | (#"\d*\.\d*\s?\+\-\s?\d*\.\d*");
<approximate-integer> ::= #"~\d+";

timestamp ::= era?, <space>, date, <whitespace>, time, <space>, time-zone?;

<date> ::= (year <"-"> month <"-"> day) | (month <" "> day <", "> year);
<duration> ::= "1440 minutes";

era ::= "A.D."
year ::= integer;
month ::= "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec";
day ::= #"[0-9]{2}";
time ::= #"\d\d:\d\d(\:\d\d\.\d\d\d\d)?";
time-zone ::= "UT"



(* Domain-specific data types *)

body-name ::= "Mars" | "Earth";
body-id ::= "499" | "399";
<spectral-fact> ::= #"\d\d \(x10\^\d\)";



(* Document proper *)

file-header ::=
    revision-date   <space+>
    body-name       <space+>
    body-id
    <" / 4">;

revision-date ::= <"Revised: "> date;


geophysical-data ::=
    <whitespace> geophysical-data-header,
    <whitespace> mean-radius,
    <whitespace> density,
    <whitespace> mass,
    <whitespace> flattening,
    <whitespace> volume,
    <whitespace> semi-major-axis,
    <whitespace> sidereal-rotation-period
    <whitespace> rotation-rate
    <whitespace> mean-solar-day
    <whitespace> polar-gravity
    <whitespace> moment-of-inertia
    <whitespace> equatorial-gravity
    <whitespace> core-radius
    <whitespace> potential-love-k2

    <whitespace> grav-spectral-fact
    <whitespace> topo-spectral-fact
    <whitespace> fig-offset
    <whitespace> offset
    <whitespace> standard-gravitational-parameter
    <whitespace> equatorial-radius
    <whitespace> gm-1-sigma
    <whitespace> mass-ratio-from-sun

    <whitespace> atmospheric-pressure
    <whitespace> maximum-angular-diameter
    <whitespace> mean-temperature
    <whitespace> visual-magnitude
    <whitespace> geometric-albedo
    <whitespace> obliquity-to-orbit
    <whitespace> mean-sidereal-orbital-period
    <whitespace> orbit-velocity
    <whitespace> mean-sidereal-orbital-period
    <whitespace> escape-velocity
    <whitespace> hill-sphere-radius
    <whitespace> magnetic-moment
    <whitespace>;

equals ::= space* "=" space*;

geophysical-data-header ::= "GEOPHYSICAL DATA (updated 2009-May-26):"

atmospheric-pressure ::= <"Atmos. pressure (bar)"> <equals> float;
core-radius ::= <"Core radius (km)"> <equals> approximate-integer;
density ::= <"Density (g cm^-3)"> <equals> float-with-error;
equatorial-gravity ::= <"Equ. gravity  ms^-2"> <equals> float;
equatorial-radius ::= <"Equatorial Radius, Re"> <equals> float <" km">;
escape-velocity ::= <"Escape vel. km/s"> <equals> float;
fig-offset ::= <"Fig. offset (Rcf-Rcm)"> <equals> float-with-error <" km">;
flattening ::= <"Flattening, f"> <equals> #"\d/\d\d\d\.\d\d\d";
geometric-albedo ::= <"Geometric albedo"> <equals> float;
gm-1-sigma ::= <"GM 1-sigma (km^3 s^-2)"> <equals> #"\+\- \d\.\d";
grav-spectral-fact ::= <"Grav spectral fact u"> <equals> spectral-fact;
hill-sphere-radius ::= <"Hill's sphere rad. Rp"> <equals> float;
magnetic-moment ::= <"Mag. mom (gauss Rp^3)"> <equals> #"< 1x10\^\-\d";
mass ::= <"Mass (10^23 kg )"> <equals> float;
mass-ratio-from-sun ::= <"Mass ratio (Sun/Mars)"> <equals> integer-with-error;
maximum-angular-diameter ::= <"Max. angular diam."> <equals> #'\d+\.\d+\"';
mean-radius ::= <"Mean radius (km)"> <equals> float-with-error;
mean-sidereal-orbital-period ::= mean-sidereal-orbital-period-years | mean-sidereal-orbital-period-days;
<mean-sidereal-orbital-period-years> ::= <"Mean sidereal orb per"> <equals> float <" y">;
<mean-sidereal-orbital-period-days> ::= <"Mean sidereal orb per"> <equals> float <" d">;
mean-solar-day ::= <"Mean solar day"> <equals> float <" d">;
mean-temperature ::= <"Mean Temperature (K)"> <equals> integer;
moment-of-inertia ::= <"Mom. of Inertia"> <equals> float;
obliquity-to-orbit ::= <"Obliquity to orbit"> <equals> float <" deg">;
offset ::= <"Offset (lat./long.)"> <equals> #"\d\dd \/ \d\dd";
orbit-velocity ::= <"Orbit vel.  km/s"> <equals> float;
polar-gravity ::= <"Polar gravity ms^-2"> <equals> float;
potential-love-k2 ::= <"Potential Love # k2"> <equals> float-with-error;
rotation-rate ::= <"Rot. Rate (x10^5 s)"> <equals> float;
semi-major-axis ::= <"Semi-major axis"> <equals> integer-with-error;
sidereal-rotation-period ::= <"Sidereal rot. period"> <equals> float <" hr">;
standard-gravitational-parameter ::= <"GM (km^3 s^-2)"> <equals> float;
topo-spectral-fact ::= <"Topo. spectral fact t"> <equals> spectral-fact;
visual-magnitude ::= <"Visual mag. V(1,0)"> <equals> float;
volume ::= <"Volume (x10^10 km^3)"> <equals> float;

ephemeris-metadata ::=
"Ephemeris / WWW_USER Mon Mar  6 16:56:26 2017 Pasadena, USA      / Horizons" <whitespace>;

targeting ::= target-body, center-body, center-site;

target-body ::= <"Target body name:"> targeting-body <whitespace>
center-body ::= <"Center body name:"> targeting-body <whitespace>
center-site ::= <"Center-site name:"> <space> "GEOCENTRIC" <whitespace>;

<targeting-body> := <space> body-name <space> <"("> body-id <")"> <space+> source;

source ::= <"{source:"> <space> "mar097" <"}">;

time-frame ::= start-time, stop-time, step-size;

start-time ::= <"Start time      : "> timestamp <whitespace>;
stop-time ::= <"Stop  time      : "> timestamp <whitespace>;
step-size ::= <"Step-size       : "> duration <whitespace>;


constants ::=
"Target pole/equ : " "IAU_MARS                        " "{East-longitude -}" <whitespace>
"Target radii    : " "3396.2 x 3396.2 x 3376.2 km     " "{Equator, meridian, pole}" <whitespace>
"Center geodetic : " "0.00000000,0.00000000,0.0000000 " "{E-lon(deg),Lat(deg),Alt(km)}" <whitespace>
"Center cylindric: " "0.00000000,0.00000000,0.0000000 " "{E-lon(deg),Dxy(km),Dz(km)}" <whitespace>
"Center pole/equ : " "High-precision EOP model        " "{East-longitude +}" <whitespace>
"Center radii    : " "6378.1 x 6378.1 x 6356.8 km     " "{Equator, meridian, pole}" <whitespace>
"Target primary  : " "Sun" <whitespace>
"Vis. interferer : " "MOON (R_eq= 1737.400) km        " "{source: mar097}" <whitespace>
"Rel. light bend : " "Sun, EARTH                      " "{source: mar097}" <whitespace>
"Rel. lght bnd GM: " "1.3271E+11, 3.9860E+05 km^3/s^2" <whitespace>
"Atmos refraction: " "NO (AIRLESS)" <whitespace>
"RA format       : " "HMS" <whitespace>
"Time format     : " "CAL" <whitespace>
"EOP file        : " "eop.170306.p170528" <whitespace>
"EOP coverage    : " "DATA-BASED 1962-JAN-20 TO 2017-MAR-06. PREDICTS-> 2017-MAY-27" <whitespace>
"Units conversion: " "1 au= 149597870.700 km, c= 299792.458 km/s, 1 day= 86400.0 s" <whitespace>
"Table cut-offs 1: " "Elevation (-90.0deg=NO ),Airmass (>38.000=NO), Daylight (NO )" <whitespace>
"Table cut-offs 2: " "Solar Elongation (  0.0,180.0=NO ),Local Hour Angle( 0.0=NO )" <whitespace>;

ephemeris-header ::= " Date__(UT)__HR:MN     R.A._(ICRF/J2000.0)_DEC  APmag  S-brt            delta      deldot    S-O-T /r    S-T-O" <whitespace>;

ephemeris ::= <"$$SOE">, <whitespace>, ephemeris-line-item+, <"$$EOE">, <whitespace>;

ephemeris-line-item ::=
    <whitespace>, timestamp,
    <whitespace>, ascension-declination,
    <whitespace>, apparent-magnitude,
    <whitespace>, surface-brightness,
    <whitespace>, range,
    <whitespace>, range-rate,
    <whitespace>, sun-observer-target-angle,
    <whitespace>, sun-observer-target-angle-direction,
    <whitespace>, sun-target-observer-angle,
    <whitespace>;

ascension-declination ::= #"\d\d \d\d \d\d\.\d\d \+\d\d \d\d \d\d\.\d";

apparent-magnitude ::= #"\d\.\d\d";

surface-brightness ::= #"\d\.\d\d";

range ::= #"\d\.\d{14}";
range-rate ::= #"\d\d\.\d{7}";
sun-observer-target-angle ::= #"\d\d\.\d\d\d\d";

sun-observer-target-angle-direction ::= "/T" | "/L";

sun-target-observer-angle ::= #"\d\d\.\d{4}";

footer ::= "Column meaning:", #"[\s\S\w\W.]*";