(* Overall document structure *)

S ::=
    <garbage*>,
    <separator>, file-header,
    (<ws>        geophysical-data)?
    (<ws>        physical-properties)?
    (<ws>        dynamical-characteristics)?
    (<separator>, <ephemeris-metadata>)?
    (<separator>, targeting)?
    (<separator>, time-frame)?
    (<separator>, constants)?
    (<separator>, <ephemeris-header>)?
    (<separator>, ephemeris)?
    (<separator>, <footer>)?
    (<separator> <#"."*> <select-prompt>)?;

(* General data types *)

<garbage> ::= #"[\d\\[\?1h\\= \^\>]+" | <ws>;
separator ::= <ws>, (short-sep | long-sep), <ws>, separator?;
short-sep ::= #"\*{79}";
long-sep ::= #"\*{110}";

space ::= #"[\s\n\r]";
ws ::= space*;

<digit> ::= #"\d";
<integer> ::= #"\d+";
<integer-with-error> ::= #"\d+\(?\+\-\d+\)?";
<float> ::= #"\-?\d*\.\d*";
<float-with-error> ::= (#"\d*\.\d*\(\d\+\-\d\)") | (#"\d*\.\d*\s?\+\-\s?\d*\.\d*");
<approximate-integer> ::= #"~\d+";

(* example: 5.1   x 10^18 *)
<sci-not> ::= #"\-?\d*\.\d* *[x\*] ?10\^\-?\d+";

timestamp ::= era?, <space>, date, <ws>, time, <space>, time-zone?;

date ::= (year <"-"> month <"-"> day) | (month <" "> day <", "> year);

era ::= "A.D."
year ::= integer;
month ::= "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec";
day ::= #"[0-9]{2}";
time ::= hour-of-day <":"> minute-of-hour (<":"> second-of-minute <"."> millisecond-of-second)?;

hour-of-day ::= #"\d\d";
minute-of-hour ::= #"\d\d";
second-of-minute ::= #"\d\d";
millisecond-of-second ::= #"\d\d\d\d";

duration ::= (hours <("h" | "hr") ws>)? (minutes <ws? ("m" | "minutes") ws?>)? (seconds <"."> milliseconds <"s">)?;
hours ::= integer;
minutes ::= integer;
seconds ::= integer;
milliseconds ::= integer;

time-zone ::= "UT"



(* Domain-specific data types *)

body-name ::= #"\w+";
body-id ::= #"\d+";
<spectral-fact> ::= #"\d\d \(x10\^\d\)";



(* Document proper *)

file-header ::=
    revision-date   <space+>
    body-name       <space+>
    body-id
    <(" / 4" | " / 1")?>;

revision-date ::= <"Revised: "> date;

physical-properties ::=
    <physical-properties-header>
    <ws> mean-radius,
    <ws> mass,
    <ws> equatorial-radius,
    <ws> <"Mass layers:">
    <ws> polar-axis
    <ws> atmospheric-mass
    <ws> flattening
    <ws> ocean-mass
    <ws> density
    <ws> crust-mass
    <ws> j2
    <ws> mantle-mass
    <ws> g-polar
    <ws> outer-core-mass
    <ws> g-equatorial
    <ws> inner-core-mass
    <ws> g-o
    <ws> fluid-core-radius
    <ws> standard-gravitational-parameter
    <ws> inner-core-radius
    <ws> rotation-rate
    <ws> <surface-area-title>
    <ws> sidereal-rotation-period
    <ws> surface-area-land
    <ws> mean-solar-day
    <ws> surface-area-sea
    <ws> moment-of-inertia
    <ws> potential-love-k2
    <ws> mean-temperature
    <ws> atmospheric-pressure
    <ws> solar-constant
    <ws> visual-magnitude
    <ws> volume
    <ws> geometric-albedo
    <ws>;

dynamical-characteristics ::=
    <dynamical-characteristics-header>
    <ws> obliquity-to-orbit
    <ws> mean-sidereal-orbital-period-years
    <ws> orbit-velocity
    <ws> mean-sidereal-orbital-period-days
    <ws> mean-daily-motion
    <ws> escape-velocity
    <ws> hill-sphere-radius
    <ws> magnetic-moment
    <ws>;

geophysical-data ::= geophysical-wip | jupiter-wip;

<jupiter-wip> ::=
"PHYSICAL DATA (updated 2009-Jan-28):
  Mass (10^24 kg)       =  1898.13+-.19   Density (g/cm^3)       =  1.326
  Equat. radius (1 bar) = 71492+-4 km     Polar radius (km)      = 66854+-10
  Volumetric mean radius= 69911+-6 km     Flattening             =  0.06487

  Rotation period       = 9h 55m 29.685s  Rot. rate(10^-4 rad/s) =  1.75865
  m = w^2a^3/GM         =  0.089195       Hydrostatic flat., fh  =  0.06509
  Inferred rot. period  =  9.894+-0.02 hr ks = 3*J2/m            =  0.494
  Mom. of inert. I/MRo^2=  0.254          I/MRo^2 (upper bound)  =  0.267
  Rocky core mass (Mc/M)=  0.0261         Y factor (He/H ratio)  =  0.18+-0.04

  GM (km^3/s^2)         = 126,686,511     GM 1-sigma (km^3/s^2)  = +-100
  Equ. grav, ge (m/s^2) = 24.79           Pol. grav, gp (m/s^2)  = 28.34

  Geometric albedo      =  0.52           Visual magnitude V(1,0)= -9.40
  Vis. mag. (opposition)= -2.70           Obliquity to orbit     =  3.12 deg
  Sidereal orbit period = 11.862615 yr    Sidereal orbit period  = 4332.820 d
  Mean daily motion     = 0.0831294 deg/d Mean orbit velocity    = 13.0697 km/s

  Atmos. temp. (1 bar)  = 165+-5 K        Heat flow/mass (x10^7) = 15 erg/gm*s
  Planetary Solar Const =  50.5 W/m^2     Dipole tilt/offset     = 9.6deg/0.1Rp
  Escape velocity (km/s)=  59.5           Mag.dip.mom(gauss-Rp^3)= 4.2
  A_roche(ice)/Rp       =  2.76           Hill's sphere rad. Rp  = 740"

<geophysical-wip> ::=
    <ws> <geophysical-data-header>,
    <ws> mean-radius,
    <ws> density,
    <ws> mass,
    <ws> flattening,
    <ws> volume,
    <ws> semi-major-axis,
    <ws> sidereal-rotation-period
    <ws> rotation-rate
    <ws> mean-solar-day
    <ws> polar-gravity
    <ws> moment-of-inertia
    <ws> equatorial-gravity
    <ws> core-radius
    <ws> potential-love-k2

    (<ws> grav-spectral-fact)?
    (<ws> topo-spectral-fact)?
    (<ws> fig-offset)?
    (<ws> offset)?
    <ws> standard-gravitational-parameter
    <ws> equatorial-radius
    <ws> gm-1-sigma
    <ws> mass-ratio-from-sun

    <ws> atmospheric-pressure
    <ws> maximum-angular-diameter
    <ws> mean-temperature
    <ws> visual-magnitude
    <ws> geometric-albedo
    <ws> obliquity-to-orbit
    <ws> mean-sidereal-orbital-period-years
    <ws> orbit-velocity
    <ws> mean-sidereal-orbital-period-days
    <ws> escape-velocity
    <ws> hill-sphere-radius
    (<ws> solar-constant)?
    (<ws> magnetic-moment)?
    (<ws> <obliquity-source>)?
    <ws>;

equals ::= space* "=" space*;

geophysical-data-header ::= "GEOPHYSICAL DATA (updated " date "):"
dynamical-characteristics-header ::= "DYNAMICAL CHARACTERISTICS:";
physical-properties-header ::= "PHYSICAL PROPERTIES (revised " date "):";

atmospheric-mass ::= <"Atmos"> <equals> sci-not <" kg">;
atmospheric-pressure ::= <("Atm." | "Atmos.") " pressure" " (bar)"?> <equals> float? <" bar"?>;
core-radius ::= <"Core radius (km)"> <equals> approximate-integer;
crust-mass ::= <"crust"> <equals> sci-not <" kg">;
density ::= <"Density", (", gm cm^-3" | " (g cm^-3)")> <equals> (float | float-with-error);
equatorial-gravity ::= <"Equ. gravity  ms^-2"> <equals> float;
equatorial-radius ::= <"Equatorial Radius, Re" | "Equ. radius, km"> <equals> (integer | float | float-with-error) <" km"?>;
escape-velocity ::= <("Escape vel. km/s" | "Escape velocity")> <equals> float <ws> <"km s^-1"?>;
fig-offset ::= <"Fig. offset (Rcf-Rcm)"> <equals> float-with-error <" km">;
flattening ::= <"Flattening" ", f"?> <equals> #"\d/\d\d\d\.\d\d\d"?;
fluid-core-radius ::= <"Fluid core rad   = "> integer <" km">;
(* equatorial & polar gravity? units seem right. What is g-o? *)
g-equatorial ::= <"ge, m s^-2 (equatorial)  = "> float;
g-o ::= <"go, m s^-2               = "> float;
g-polar ::= <"gp, m s^-2 (polar)       = "> float;
geometric-albedo ::= <"Geometric albedo"> <equals> float;
gm-1-sigma ::= <"GM 1-sigma (km^3 s^-2)"> <equals> #"\+\- ?\d\.\d+";
grav-spectral-fact ::= <"Grav spectral fact u"> <equals> spectral-fact;
hill-sphere-radius ::= <("Hill's sphere rad. Rp" | "Hill's sphere radius")> <equals> float;
inner-core-mass ::= <"inner core     = "> sci-not <" kg">;
inner-core-radius ::= <"Inner core rad   = "> integer <" km">;
j2 ::= <"J2  (GEM T2, 1990)       = "> float;
magnetic-moment ::= <("Mag. mom (gauss Rp^3)" | "Magnetic moment")> <equals> (#"< 1x10\^\-\d" | (float <ws> <"gauss Rp^3">));
mantle-mass ::= <"mantle         = "> sci-not <" kg">;
mass ::= <"Mass (10^23 kg )" | "Mass, 10^24 kg">  <equals> (float | float-with-error);
mass-ratio-from-sun ::= <"Mass ratio " ("(Sun/Mars)" | "(sun/plnt)")> <equals> (integer | integer-with-error);
maximum-angular-diameter ::= <"Max. angular diam."> <equals> #'\d+\.\d+\"';
mean-daily-motion ::= <"Mean daily motion, n"> <equals> float <ws> <"deg/d">;
mean-radius ::= <"Mean radius" ws ", "? "("? "km" ")"?> <equals> (float-with-error | integer-with-error);
mean-sidereal-orbital-period-years ::= <("Mean sidereal orb per" | "Sidereal period" | "Sidereal orb. per.")> <equals> float  <ws> <("yrs" | "y")?>;
mean-sidereal-orbital-period-days ::= <("Mean sidereal orb per" | "Sidereal period" | "Sidereal orb. per.")> <equals> float <ws> <("d" | "days")>;
mean-solar-day ::= <"Mean solar day" ", days"?> <equals> float <" d"?>;
mean-temperature ::= <"Mean Temperature" (" (K)" | ", K")> <equals> integer?;
moment-of-inertia ::= <("Mom. of Inertia" | "Moment of inertia")> <equals> float;
ocean-mass ::= <"oceans"> <equals> "1.4   x 10^21" <" kg">;
obliquity-to-orbit ::= <"Obliquity to orbit"> <"[1]"?> <", deg"?> <equals> (float | #"\d.\d\d' \+/\- \d\.\d'") <" deg"?>;
offset ::= <"Offset (lat./long.)"> <equals> #"\d\dd \/ \d\dd";
orbit-velocity ::= <("Orbit vel.  km/s" | "Orbit velocity, km s^-1" | "Mean Orbit vel.  km/s")> <equals> float;
outer-core-mass ::= <"outer core     = "> sci-not <" kg">;
polar-gravity ::= <"Polar gravity ms^-2"> <equals> float?;
polar-axis ::= <"Polar axis, km"> <equals> float;
potential-love-k2 ::= <("Potential Love # k2" | "Love no., k2")> <equals> (float | float-with-error)?;
rotation-rate ::= <("Rot. Rate (x10^5 s)" | "Mean rot. rate, rad s^-1")> <equals> (float | sci-not);
semi-major-axis ::= <"Semi-major axis"> <equals> integer-with-error?;
solar-constant ::= <("Solar constant, W/m^2" | "Planetary Solar Const")> <equals> float  <(ws "(Wm^2)")?>;
sidereal-rotation-period ::= <("Sidereal rot. period" | "Sidereal period, hr" | "Sidereal period")> <equals> float <ws> <("hr" | "d")?>;
standard-gravitational-parameter ::= <"GM" (", km^3 s^-2" | " (km^3 s^-2)")> <equals> float;
surface-area-title ::= "Surface Area:";
surface-area-land ::= <"land           = "> sci-not <" km">;
surface-area-sea ::= <"sea            = "> sci-not <" km">;
topo-spectral-fact ::= <"Topo. spectral fact t"> <equals> spectral-fact;
visual-magnitude ::= <("Visual" | "Vis.") " mag. V(1,0)"> <equals> float;
volume ::= <("Volume (x10^10 km^3)" | "Volume, 10^10 km^3")> <equals> float;

ephemeris-metadata ::=
"Ephemeris / WWW_USER Mon Mar  6 16:56:26 2017 Pasadena, USA      / Horizons" <ws>;

targeting ::= target-body, center-body, center-site;

target-body ::= <"Target body name:"> targeting-body <ws>
center-body ::= <"Center body name:"> targeting-body <ws>
center-site ::= <"Center-site name:"> <space> "GEOCENTRIC" <ws>;

<targeting-body> := <space> body-name <space> <"("> body-id <")"> <space+> source;

source ::= <"{source:"> <space> "mar097" <"}">;
obliquity-source ::= "[1] Margot et al., Science 316, 2007";

time-frame ::= start-time, stop-time, step-size;

start-time ::= <"Start time      : "> timestamp <ws>;
stop-time ::= <"Stop  time      : "> timestamp <ws>;
step-size ::= <"Step-size       : "> duration <ws>;

colon ::= ws ":" ws;
by ::= ws "x" ws;

constants ::=
    target-pole-equ,
    target-radii,
    center-geodetic,
    center-cylindric,
    center-pole-equ,
    center-radii,
    target-primary,
    visibility-interferer,
    rel-light-bend,
    rel-light-bend-gm,
    atmospheric-refraction,
    ra-format,
    time-format,
    eop-file,
    eop-coverage,
    units-conversion,
    table-cutoffs-1,
    table-cutoffs-2;


target-pole-equ ::= <"Target pole/equ"> <colon> "IAU_MARS" <ws> <"{East-longitude -}"> <ws>;
target-radii ::= <"Target radii"> <colon> equator <by> meridian <by> pole <" km"> <ws> <"{Equator, meridian, pole}"> <ws>;
center-geodetic ::= <"Center geodetic"> <colon> longitude <","> latitude <","> altitude <ws> <"{E-lon(deg),Lat(deg),Alt(km)}"> <ws>;
center-cylindric ::= <"Center cylindric"> <colon> longitude <","> xy-dimension <","> z-dimension <ws> <"{E-lon(deg),Dxy(km),Dz(km)}"> <ws>;
center-pole-equ ::= <"Center pole/equ"> <colon> "High-precision EOP model" <ws> <"{East-longitude +}"> <ws>;
center-radii ::= <"Center radii"> <colon> "6378.1 x 6378.1 x 6356.8 km" <ws> <"{Equator, meridian, pole}"> <ws>;
target-primary ::= <"Target primary"> <colon> "Sun" <ws>;
visibility-interferer ::= <"Vis. interferer"> <colon> "MOON (R_eq= 1737.400) km" <ws> <source> <ws>;
rel-light-bend ::= <"Rel. light bend"> <colon> "Sun, EARTH" <ws> <source> <ws>;
rel-light-bend-gm ::= <"Rel. lght bnd GM"> <colon> "1.3271E+11, 3.9860E+05 km^3/s^2" <ws>;
atmospheric-refraction ::= <"Atmos refraction"> <colon> "NO (AIRLESS)" <ws>;
ra-format ::= <"RA format"> <colon> "HMS" <ws>;
time-format ::= <"Time format"> <colon> "CAL" <ws>;
eop-file ::= <"EOP file"> <colon> "eop.170306.p170528" <ws>;
eop-coverage ::= <"EOP coverage"> <colon> "DATA-BASED 1962-JAN-20 TO 2017-MAR-06. PREDICTS-> 2017-MAY-27" <ws>;
units-conversion ::= <"Units conversion"> <colon> "1 au= 149597870.700 km, c= 299792.458 km/s, 1 day= 86400.0 s" <ws>;
table-cutoffs-1 ::= <"Table cut-offs 1"> <colon> "Elevation (-90.0deg=NO ),Airmass (>38.000=NO), Daylight (NO )" <ws>;
table-cutoffs-2 ::= <"Table cut-offs 2"> <colon> "Solar Elongation (  0.0,180.0=NO ),Local Hour Angle( 0.0=NO )" <ws>;

equator ::= float;
meridian ::= float;
pole ::= float;

longitude ::= float;
latitude ::= float;
altitude ::= float

xy-dimension ::= float;
z-dimension ::= float;

ephemeris-header ::= " Date__(UT)__HR:MN     R.A._(ICRF/J2000.0)_DEC  APmag  S-brt            delta      deldot    S-O-T /r    S-T-O" <ws>;

ephemeris ::= <"$$SOE">, <ws>, ephemeris-line-item+, <"$$EOE">, <ws>;

ephemeris-line-item ::=
    <ws>, measurement-time,
    <ws>, ascension-declination,
    <ws>, apparent-magnitude,
    <ws>, surface-brightness,
    <ws>, range,
    <ws>, range-rate,
    <ws>, sun-observer-target-angle,
    <ws>, sun-observer-target-angle-direction,
    <ws>, sun-target-observer-angle,
    <ws>;

measurement-time ::= timestamp;

ascension-declination ::= #"\d\d \d\d \d\d\.\d\d \+\d\d \d\d \d\d\.\d";

apparent-magnitude ::= #"\d\.\d\d";

surface-brightness ::= #"\d\.\d\d";

range ::= #"\d\.\d{14}";
range-rate ::= #"\d\d\.\d{7}";
sun-observer-target-angle ::= #"\d\d\.\d\d\d\d";

sun-observer-target-angle-direction ::= "/T" | "/L";

sun-target-observer-angle ::= #"\d\d\.\d{4}";

select-prompt ::= "Select ... [E]phemeris, [F]tp, [M]ail, [R]edisplay, ?, <cr>:" <ws>;

footer ::= "Column meaning:", #"[\s\S\w\W.]*";
